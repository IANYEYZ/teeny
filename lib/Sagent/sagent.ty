include("./llm.ty");

sql.init("./medical_system.db");

loadPrompt := (filePath) => {
    fs.readText("./prompt/{filePath}")
};

tools := [    # All tools that agent can use
    "Calculator": (expr) => {
        return eval(expr)
    },
    "Plan": (goal) => {
        llmInstance := llm();
        llmInstance.addMessage("system", loadPrompt("PLANNER"));
        llmInstance.addMessage("user", "make a plan for the goal: {goal}");
        res := llmInstance.response();
        res
    },
    "ReadFile": (filePath) => {
        println("Reading from file: {filePath}");
        try {
            fs.readText(filePath)
        } catch (e) => {
            println("Error occured: {e}")
            "{e}"
        }
    },
    "WriteFile": (filePathAndContent) => {
        println("Writing to file: {filePathAndContent['path']}");
        filePath := filePathAndContent["path"];
        content := filePathAndContent["content"];
        fs.writeText(filePath, content);
        nil
    },
    "Command": (cmd) => {
        println("Running command: {cmd}")
        os.run(cmd)
    },
    "API": (prop) => {
        println("Running commands: {prop['type']}")
        res := match prop["type"] {
            "Weather": "sunny",
            "Date": "2025-12-21",
            "Database": sql.execute(prop["params"]),
            _: "No such API"
        }
    },
    "Script": (code) => {
        println("Running Python script")
        fs.writeText("./tempPythonFile.py", code)
        os.run("python tempPythonFile.py")
    }
];
main := () => {
    llmInstance := llm();
    llmInstance.addMessage("system", loadPrompt("SYSTEM"));
    while 1 {
        print(">>> ");
        rd := input();
        if rd == ":exit" {
            break;
        }
        rd = json.encode([
            "type": "Message",
            "data": rd
        ])
        res := ["type": "Init", "data": ""];
        while res["type"] != "END" {
            res := json.decode(llmInstance.addMessage("user", rd).response());
            match res["type"] {
                "Message": {
                    print("Assistant: {res['data']}\n")
                    rd = json.encode([
                        "type": "Result",
                        "data": nil
                    ])
                },
                "END": {
                    0
                },
                _: {
                    println(res);
                    toolRes := tools[res["type"]](res["data"]);
                    rd = json.encode([
                        "type": "Result",
                        "data": toolRes
                    ])
                }
            }
            if res["type"] == "END" {
                break
            }
        }
    }
};

main()